// =================================
// Copyright (c) 2019 Seppo Laakko
// Distributed under the MIT license
// =================================

[hpp]#include <soulng/parsing/Parser.hpp>
[hpp]#include <soulng/parsing/Scope.hpp>
[cpp]#include <soulng/code/Expression.hpp>
[cpp]#include <soulng/code/Statement.hpp>
[cpp]#include <soulng/syntax/Primitive.hpp>
[cpp]#include <soulng/syntax/Composite.hpp>
[cpp]#include <soulng/syntax/Element.hpp>
[cpp]#include <soulng/parsing/Nonterminal.hpp>
[cpp]#include <soulng/cppcode/Statement.hpp>

namespace soulng.syntax
{
    [cpp]using namespace soulng::code;

    grammar Primary
    {
        using Primitive.Primitive;
        using Element.Identifier;
        using Composite.Alternative;
        using soulng.code.Expression.ExpressionList;
        using soulng.code.Statement.CompoundStatement;

        Primary(soulng::parsing::Scope* enclosingScope): soulng::parsing::Parser*
                    ::=
                    (   RuleCall{ value = RuleCall; }
                    |   Primitive{ value = Primitive; }
                    |   Grouping(enclosingScope){ value = Grouping; }
                    |   Token(enclosingScope){ value = Token; }
                    )
                    (Expectation(value){ value = Expectation; })?
                    (Action(value){ value = Action; })?
                    ;

        RuleCall: soulng::parsing::Parser*
                    ::= ((token(Nonterminal:ntWithArgs '(') ExpressionList ')' | Nonterminal:nt) Alias?)
                    {
                        std::u32string ruleName;
                        if (!ntWithArgs.empty())
                        {
                            ruleName = ntWithArgs;
                        }
                        else
                        {
                            ruleName = nt;
                        }
                        std::u32string alias = Alias;
                        std::u32string name = !alias.empty() ? alias : ruleName;
                        NonterminalParser* parser(new NonterminalParser(name, ruleName));
                        parser->SetSpan(span);
                        if (!ExpressionList.empty())
                        {
                            parser->SetArguments(ExpressionList); 
                        }
                        value = parser;
                    }
                    ;

        Nonterminal: std::u32string 
                    ::= Identifier{ value = Identifier; }
                    ;

        Alias: std::u32string
                    ::= ':' Identifier!{ value = Identifier; }
                    ;

        Grouping(soulng::parsing::Scope* enclosingScope): soulng::parsing::Parser*
                    ::= '(' Alternative(enclosingScope)! ')'!{ value = new GroupingParser(Alternative); }
                    ;

        Token(soulng::parsing::Scope* enclosingScope): soulng::parsing::Parser*
                    ::= keyword("token") '('! Alternative(enclosingScope)! ')'!{ value = new TokenParser(Alternative); }
                    ;

        Expectation(soulng::parsing::Parser* child): soulng::parsing::Parser* 
                    ::= '!'{ value = new ExpectationParser(child); }
                    ;

        Action(soulng::parsing::Parser* child): soulng::parsing::Parser*
                    ::= (CompoundStatement:action ('/' CompoundStatement:failureAction)?)
                    {
                        value = new ActionParser(U"", action, failureAction, child);
                    }
                    ;
    }
}
