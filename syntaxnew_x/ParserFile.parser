// =================================
// Copyright (c) 2019 Seppo Laakko
// Distributed under the MIT license
// =================================

[cpp]#include <soulng/parsing/Namespace.hpp>
[cpp]#include <soulng/parsing/Grammar.hpp>
[cpp]#include <soulng/code/Declaration.hpp>
[hpp]#include <soulng/syntax/ParserFileContent.hpp>
[cpp]#include <soulng/syntax/Element.hpp>
[cpp]#include <soulng/syntax/Grammar.hpp>

namespace soulng.syntax
{
    grammar ParserFile
    {
        using stdlib.spaces_and_comments;
        using stdlib.newline;
        using stdlib.qualified_id;
        using GrammarGrammar.Grammar;
        using soulng.code.Declaration.UsingDeclaration;
        using soulng.code.Declaration.UsingDirective;
        using soulng.code.Declaration.NamespaceAliasDefinition;

        skip spaces_and_comments;

        ParserFile(int id_, soulng::parsing::ParsingDomain* parsingDomain_): ParserFileContent*
                    ::= empty{ value = new ParserFileContent(id_, parsingDomain_); value->SetFilePath(fileName); }
                        IncludeDirectives(value)?
                        NamespaceContent(value)
                    ;

        IncludeDirectives(ParserFileContent* parserFileContent)
                    ::= IncludeDirective(parserFileContent)+
                    ;

        IncludeDirective(ParserFileContent* parserFileContent)
                    ::= FileAttribute? '#' keyword("include") token(IncludeFileName [^\r\n]* newline){ parserFileContent->AddIncludeDirective(ToUtf8(IncludeFileName), ToUtf8(FileAttribute)); }
                    ;

        FileAttribute: std::u32string
                    ::= '[' ("cpp" | "hpp"){ value = std::u32string(matchBegin, matchEnd); } ']'
                    ;

        IncludeFileName: std::u32string
                    ::= token('<' [^>\r\n]+ '>'){ value = std::u32string(matchBegin, matchEnd); }
                    ;

        NamespaceContent(ParserFileContent* parserFileContent)
                    ::= 
                    (   
                        FileAttribute? 
                        (   UsingDeclaration{ UsingDeclaration->SetFileAttr(FileAttribute); parserFileContent->AddUsingObject(UsingDeclaration); }
                        |   UsingDirective{ UsingDirective->SetFileAttr(FileAttribute); parserFileContent->AddUsingObject(UsingDirective); }
                        |   NamespaceAliasDefinition{ NamespaceAliasDefinition->SetFileAttr(FileAttribute); parserFileContent->AddUsingObject(NamespaceAliasDefinition); }
                        )
                    |   Grammar(parserFileContent->CurrentScope()){ parserFileContent->AddGrammar(Grammar); }
                    |   Namespace(parserFileContent)
                    )*
                    ;

        Namespace(ParserFileContent* parserFileContent)
                    ::= keyword("namespace") qualified_id:ns!{ parserFileContent->BeginNamespace(ns); }
                        '{'!
                        NamespaceContent(parserFileContent)
                        '}'!{ parserFileContent->EndNamespace(); }
                    ;
    }
}
