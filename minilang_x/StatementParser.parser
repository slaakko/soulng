#include <soulng/minilang/ExpressionParser.hpp>
#include <soulng/minilang/TypeParser.hpp>
#include <soulng/minilang/IdentifierParser.hpp>
#include <soulng/minilang/MinilangLexer.hpp>
#include <soulng/minilang/MinilangTokens.hpp>

using namespace MinilangTokens;

parser MinilangStatementParser
{
    uselexer MinilangLexer;

    using MinilangExpressionParser.Expression;
    using MinilangTypeParser.Type;
    using MinilangIdentifierParser.Identifier;

    Statement
        ::= IfStatement:ifS
        |   WhileStatement:whileS
        |   ReturnStatement:returnS
        |   ConstructionStatement:constructionS
        |   AssignmentStatement:assignmentS
        |   CompoundStatement:compoundS
        ;

    IfStatement
        ::= IF LPAREN Expression:condition RPAREN Statement:thenS (ELSE Statement:elseS)?
        ;

    WhileStatement
        ::= WHILE LPAREN Expression:condition RPAREN Statement:statement
        ;

    ReturnStatement
        ::= RETURN Expression:returnValue? SEMICOLON
        ;

    ConstructionStatement
        ::= Type:type Identifier:variableName ASSIGN Expression:value SEMICOLON
        ;

    AssignmentStatement
        ::= Identifier:variableName ASSIGN Expression:value SEMICOLON
        ;

    CompoundStatement
        ::= LBRACE Statement:statement* RBRACE
        ;
}
