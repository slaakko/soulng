namespace System
{
    public class RandomAccessIter<T, R, P>
    {
        public typedef T ValueType;
        public typedef R ReferenceType;
        public typedef P PointerType;
        private typedef RandomAccessIter<ValueType, ReferenceType, PointerType> Self;
        public inline nothrow RandomAccessIter() :
            ptr(null)
        {
        }
        public inline explicit nothrow RandomAccessIter(PointerType ptr_) :
            ptr(ptr_)
        {
        }
        public inline nothrow Self& operator++()
        {
            #assert (ptr != null);
            ++ptr;
            return *this;
        }
        public inline nothrow Self& operator--()
        {
            #assert (ptr != null);
            --ptr;
            return *this;
        }
        public inline nothrow ReferenceType operator*()
        {
            #assert (ptr != null);
            return *ptr;
        }
        public inline nothrow PointerType operator->()
        {
            #assert (ptr != null);
            return ptr;
        }
        public inline nothrow ReferenceType operator[](long index)
        {
            #assert (ptr != null);
            return ptr[index];
        }
        public inline nothrow PointerType Ptr() const
        {
            return ptr;
        }
        private PointerType ptr;
    }
    public inline nothrow RandomAccessIter<T, R, P> operator+<T, R, P>(const RandomAccessIter<T, R, P>& it, long offset)
    {
        #assert (it.Ptr() != null);
        return RandomAccessIter<T, R, P>(it.Ptr() + offset);
    }
    public inline nothrow RandomAccessIter<T, R, P> operator+<T, R, P>(long offset, const RandomAccessIter<T, R, P>& it)
    {
        #assert (it.Ptr() != null);
        return RandomAccessIter<T, R, P>(it.Ptr() + offset);
    }
    public inline nothrow RandomAccessIter<T, R, P> operator-<T, R, P>(const RandomAccessIter<T, R, P>& it, long offset)
    {
        #assert (it.Ptr() != null);
        return RandomAccessIter<T, R, P>(it.Ptr() - offset);
    }
    public inline nothrow long operator-<T, R, P>(const RandomAccessIter<T, R, P>& left, const RandomAccessIter<T, R, P>& right)
    {
        #assert (left.Ptr() == null && right.Ptr() == null || left.Ptr() != null && right.Ptr() != null);
        if (left.Ptr() == null && right.Ptr() == null)
        {
            return 0;
        }
        return left.Ptr() - right.Ptr();
    }
    public inline nothrow bool operator==<T, R, P>(const RandomAccessIter<T, R, P>& left, const RandomAccessIter<T, R, P>& right)
    {
        return left.Ptr() == right.Ptr();
    }
    public inline nothrow bool operator<<T, R, P>(const RandomAccessIter<T, R, P>& left, const RandomAccessIter<T, R, P>& right)
    {
        #assert (left.Ptr() == null && right.Ptr() == null || left.Ptr() != null && right.Ptr() != null);
        return left.Ptr() < right.Ptr();
    }
} // namespace System
